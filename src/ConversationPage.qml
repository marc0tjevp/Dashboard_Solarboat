import QtQuick 2.7
import QtQuick.Controls 2.2
import QtQuick.Layouts 1.3
import io.qt.examples.chattutorial 1.0

Page {
    id: root
    anchors.fill: parent

    property string inConversationWith: "Ernest Hemingway"

    ColumnLayout {
        anchors.fill: parent

        ListView {
            id: listView
            Layout.fillWidth: true
            Layout.fillHeight: true
            Layout.margins: pane.leftPadding + messageField.leftPadding
            displayMarginBeginning: 40
            displayMarginEnd: 40
            verticalLayoutDirection: ListView.BottomToTop
            spacing: 12
            model: SqlConversationModel {
                recipient: inConversationWith
            }
            delegate: Column {
                anchors.right: sentByMe ? parent.right : undefined
                spacing: 6

                readonly property bool sentByMe: model.recipient !== "Me"

                Row {
                    id: messageRow
                    spacing: 6
                    anchors.right: sentByMe ? parent.right : undefined

                    Image {
                        id: avatar
                        source: !sentByMe ? "qrc:///img/" + model.author.replace(" ", "_") + ".png" : ""
                    }

                    Rectangle {
                        width: Math.min(messageText.implicitWidth + 24,
                            listView.width - (!sentByMe ? avatar.width + messageRow.spacing : 0))
                        height: messageText.implicitHeight + 24
                        color: sentByMe ? "lightgrey" : "#C6002A"

                        Label {
                            id: messageText
                            text: model.message
                            color: sentByMe ? "black" : "white"
                            anchors.fill: parent
                            anchors.margins: 12
                            wrapMode: Label.Wrap
                        }
                    }
                }

                Label {
                    id: timestampText
                    text: Qt.formatDateTime(model.timestamp, "d MMM hh:mm")
                    color: "darkgray"
                    anchors.right: sentByMe ? parent.right : undefined
                }
            }

            ScrollBar.vertical: ScrollBar { width: 5}
        }

        Pane {
            id: pane
            Layout.fillWidth: true

            RowLayout {
                width: parent.width

                TextArea {
                    id: messageField
                    Layout.fillWidth: true
                    placeholderText: qsTr("Compose message")
                    wrapMode: TextArea.Wrap
                    onFocusChanged:
                    {
                        if (messageField.focus == true) {
                            root.height = 200;
                        } else {
                            root.height = 440;
                        }
                    }
                }

                Button {
                    id: sendButton
                    text: messageField.length > 0 ? "Send" : "Hide"
                    enabled: true
                    onPressed: {
                        if(messageField.length > 0) {
                            listView.model.sendMessage(inConversationWith, messageField.text);
                            messageField.text = "";
                        }
                    }
                }
            }
        }
    }
//    Button {
//        text: qsTr("Back")
//        anchors.left: parent.left
//        anchors.leftMargin: 10
//        anchors.top: parent.top
//        anchors.topMargin: 10
//        onClicked: root.StackView.view.pop()
//    }
}
